-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun binop (l, op, r) {
  Binop (op, l, r)
}

-- Primary expression
var primary = memo $ eta syntax (x=decimal {Const (stringInt (x))} |
                                 x=lident  {Var (x)}               |
                                 inbr[s("("), exp, s(")")]),
      exp = memo $ eta expr({
        [Left, {[s ("!!"), binop]}],
        [Left, {[s ("&&"), binop]}],
        [Nona, {[s ("=="), binop], [s ("!="), binop], [s ("<="), binop], [s (">="), binop], [s ("<"), binop], [s (">"), binop]}],
        [Left, {[s ("+"), binop], [s ("-"), binop]}],
        [Left, {[s ("*"), binop], [s ("/"), binop], [s ("%"), binop]}]
      }, primary);

var elseSequence = memo $ eta syntax (
  kElse body=stmt {body}
  | kElif cond=exp kThen trueBranch=stmt falseBranch=elseSequence? {If (cond, trueBranch, 
    case falseBranch of
      Some(body) -> body
    | None -> Skip
    esac
  )}
);

var primaryStmt = memo $ eta syntax (
    kSkip {Skip}
  | kRead v=inbr[s("("), lident, s(")")] {Read (v)}
  | kWrite e=inbr[s("("), exp, s(")")] {Write (e)}
  | v=lident s[":="] e=exp { Assn (v, e) }
  | kIf cond=exp kThen trueBranch=stmt falseBranch=elseSequence? kFi {If (cond, trueBranch, 
    case falseBranch of
      Some(body) -> body
    | None -> Skip
    esac
  )}
  | kWhile cond=exp kDo body=stmt kOd {While (cond, body)}
  | kDo body=stmt kWhile cond=exp kOd {DoWhile (cond, body)}
  | kFor init=stmt s[","] cond=exp s[","] iter=stmt kDo body=stmt kOd {Seq (init, While (cond, Seq (body, iter)))}
);

public stmt = memo $ eta syntax (
  primaryStmt
  | head=primaryStmt s[";"] tail=stmt {Seq (head, tail)}
);
             
-- Public top-level parser
public parse = stmt;
